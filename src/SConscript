import packaging
import subprocess

env=Environment(tools=["default","textfile",git_tag_version,get_architecture_from_dpkg])


# flags
env.Append(CCFLAGS="-Wall")
env.Append(CCFLAGS="-std=c++11")
env.Append(CCFLAGS="-DBOOST_ALL_DYN_LINK")


# DEBUGGING
debug=ARGUMENTS.get("debug",0)
if int(debug):
	env.Append(CCFLAGS="-g")


backend_libs = []
backend_sources = []
conf=Configure(env)

# RASPBERRY PI
if conf.CheckCHeader("wiringPi.h"):
	backend_sources.append("backends/raspi.cpp")
else:
	fake_wiringpi=bool(ARGUMENTS.get("fake_wiringpi",0))
	if fake_wiringpi:
		env_fake = env.Clone()
		env_fake.Append(CCFLAGS="-DFAKE_WIRINGPI")
		backend_sources.append(env_fake.Object("backends/raspi.cpp"))



env=conf.Finish()

# PYTHON
import distutils.sysconfig
env.Append(CPPPATH=[distutils.sysconfig.get_python_inc()])
pythonlib="python"+distutils.sysconfig.get_config_var("VERSION")


# LIBS
libs=[
"wt",
"wthttp",
"boost_system",
"boost_thread",
"boost_filesystem",
"boost_log",
"boost_log_setup",
"pthread",
"boost_signals",
"boost_python"
]
libs+=[pythonlib]


# SOURCES
sources_widgets=[
"widgets/EndpointWidget.cpp",
"widgets/OnOffButtonWidget.cpp",
"widgets/SliderWidget.cpp"
]
sources_python=Glob("python/*.cpp")

sources_daemon=[
"constants.cpp",
"Endpoint.cpp",
"ScalarEndpoint.cpp",
"HardwareEndpoint.cpp",
"exceptions.cpp",
"heinzd.cpp",
"heinz.cpp",
"EndpointListWidget.cpp",
"WebApp.cpp",
"config/config_loader.cpp",
"MultiplexerEndpoint.cpp",

"plugin.cpp",
"backends/FakeBackend.cpp"
]

sources_daemon+=sources_widgets
sources_daemon+=sources_python
sources_daemon+=backend_sources
libs+=backend_libs

# version information
env.Substfile(target="version.hpp",source="version.hpp.in")

# program
heinzd=env.Program(target="heinzd",source=sources_daemon,LIBS=libs)

# config and installation
config_main=env.Command("heinz.conf","../config/example.conf",Copy("$TARGET", "$SOURCE"))
config_http=env.Command("http.conf","../config/http.conf",Copy("$TARGET", "$SOURCE"))
config_service=env.File("../heinz.service")


# install bin
install_bin=env.Install("/usr/bin",heinzd)
install_systemd_script=Install("/lib/systemd/system",config_service)
install_config=InstallAs("/etc/heinz/heinz.conf",config_main)
install_config2=InstallAs("/etc/heinz/http.conf",config_http)

env.Alias("install",[install_bin,install_systemd_script,install_config,install_config2])

# package
packagename="heinz"
package=packaging.make_debian(packagename,[
	(heinzd,"/usr/bin/heinzd"),
	(config_main,"/etc/heinz/heinz.conf"),
	(config_http,"/etc/heinz/http.conf"),
	(config_service,"/lib/systemd/system/heinz.service"),
	("../LICENSE","/usr/share/doc/{}/copyright".format(packagename))
	],env)
env.Alias("package",package)
# default
Default(heinzd)
